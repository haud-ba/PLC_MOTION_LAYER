<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_CamCtrl" Id="{527605ca-3a86-0df1-134f-058287ba4975}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_CamCtrl EXTENDS FB_CamAxis
VAR
  _eInit                    : E_PROGRESS;
  _nAxisIndex               : UINT;       // know thyself

  // get interface from McAxis for access to Reset, Enable, Disable, Halt
  _ItfMcAxis                : ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF I_McAxis;

  // pointers to all; access required for coupling; access by index
  _Axes                     : REFERENCE TO ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF AXIS_REF;

  _Ctrl                     : POINTER TO ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF ST_CAM_CTRL;    // contains command to be executed
  _State                    : POINTER TO ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF ST_CAM_STATE;   // mirrors command with added result

  _Info                     : POINTER TO ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF ST_CAM_INFO;    // axis state and position data to read from
  _Data                     : POINTER TO ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF ST_CAM_DATA;

  // local copies of command
  _eCtrl,
  _eCtrlOld                       : E_AXIS_CTRL;

  // execution state
  _eResult                        : E_PROGRESS;   // result of execution state
  _eResultOld                     : E_PROGRESS;   // old result for detecting change
  _eState                         : E_AXIS_STATE; // execution state
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="methods" Id="{4ccfdf09-b364-0c1d-3e7c-34e0e2de610e}">
      <Folder Name="private" Id="{2c963b3f-11dc-0f83-344f-30cd3b18b2d7}" />
    </Folder>
    <Folder Name="properties" Id="{b1030b9c-4a99-0110-1bdc-fcc7e22c6236}" />
    <Property Name="Axes" Id="{99b51e40-6ba2-0269-3ced-06a41985b0ef}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY Axes : reference TO ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF AXIS_REF]]></Declaration>
      <Set Name="Set" Id="{30e3614d-b3b9-0180-2b52-88caa8caecb6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Axes := Axes;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="AxisCamIdle" Id="{82b7661b-cba3-059c-3469-a680d4228644}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD AxisCamIdle : E_PROGRESS
VAR_INPUT
  Execute           : BOOL;
END_VAR
VAR_INST
  _eResult,
  _eState           : E_PROGRESS;
  _stMsg            : ST_Message;
  _nRetry           : UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT Check() THEN _eState := E_PROGRESS.ERROR; END_IF
IF NOT Execute THEN _eState := E_PROGRESS.INIT; END_IF

CASE _eState
OF
  E_PROGRESS.INIT:
    IF Execute 
    THEN
      memset(ADR(_stMsg),0,SIZEOF(_stMsg));
      _nRetry                       := 0;
      _eResult                      := _ItfMcAxis[_nAxisIndex].Reset(FALSE);
      _eState                       := E_PROGRESS.BUSY;
    END_IF
END_CASE
CASE _eState
OF
  E_PROGRESS.BUSY:
    _eResult                        := _ItfMcAxis[_nAxisIndex].Reset(TRUE);

    IF (_eResult = E_PROGRESS.DONE)
    THEN
      _nRetry                       := _nRetry + 1;
      _eState                       := E_PROGRESS.STARTUP;
    END_IF
END_CASE
CASE _eState
OF
  E_PROGRESS.STARTUP:
    _eResult                        := _ItfMcAxis[_nAxisIndex].Reset(FALSE);
    _eResult                        := _ItfMcAxis[_nAxisIndex].Enable(FALSE);
    _eState                         := E_PROGRESS.READY;
END_CASE
CASE _eState
OF
  E_PROGRESS.PREPARE:
    _eResult                        := _ItfMcAxis[_nAxisIndex].Reset(FALSE);
    _eResult                        := _ItfMcAxis[_nAxisIndex].Enable(FALSE);

    _eState                         := E_PROGRESS.BUSY;

    _stMsg.eType                    := E_MessageType.eMessageInfo;
    _stMsg.eDevice                  := e_Device.Axis + TO_DINT(_AxisSlave.NcToPlc.AxisId);
    _stMsg.eSubdevice               :=  e_SubDevice.AxisControl_Enable;
    _stMsg.iErrorNumber             := _nErrorId;

    _stMsg.sText                    := concat(TO_STRING(_eCtrl), ' retry: ');
    _stMsg.sText                    := concat(_stMsg.sText, TO_STRING(_nRetry));

    f_MessageSet(_stMsg);
END_CASE
CASE _eState
OF
  E_PROGRESS.READY:
    _eResult                        := _ItfMcAxis[_nAxisIndex].Enable(TRUE);

    IF (_nRetry > MAX_RETRY)
    THEN
      _eState                       := E_PROGRESS.ERROR;

    ELSIF (_eResult = E_PROGRESS.DONE)
    THEN
      _ItfMcAxis[_nAxisIndex].Enable(FALSE);
      _eState                       := E_PROGRESS.DONE;

    ELSIF (_eResult = E_PROGRESS.ERROR)
    THEN
      _eState                       := E_PROGRESS.PREPARE;

      _nErrorId                     := _AxisSlave.NcToPlc.ErrorCode;
    
      _stMsg.eType                  := E_MessageType.eMessageError;
      _stMsg.eDevice                := e_Device.Axis + TO_DINT(_AxisSlave.NcToPlc.AxisId);
      _stMsg.eSubdevice             :=  e_SubDevice.AxisControl_Enable;
      _stMsg.iErrorNumber           := _nErrorId;

      _stMsg.sText                  := concat(TO_STRING(_eCtrl), ' error axis enable: ');
      _stMsg.sText                  := concat(_stMsg.sText, TO_STRING(_nAxisIndex));

      f_MessageSet(_stMsg);
    END_IF
END_CASE

AxisCamIdle                                := _eState;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="AxisCamIn" Id="{35ea102d-b0ce-0325-34b7-88c7280ba992}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD AxisCamIn : E_PROGRESS
VAR_INPUT
  Execute           : BOOL;
END_VAR
VAR_INST
  _eCk              : E_CHECK_STATE;

  _eResult,
  _eState           : E_PROGRESS;
  _rValue           : LREAL;
  _udiErr           : UDINT;
  _stMsg            : ST_Message;

END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT Check() THEN _eState := E_PROGRESS.ERROR; END_IF
IF NOT Execute THEN _eState := E_PROGRESS.INIT; END_IF


CASE _eState
OF
  E_PROGRESS.INIT:
    IF Execute 
    THEN
      memset(ADR(_stMsg),0,SIZEOF(_stMsg));
      _udiErr                       := 0;

      IF NOT (_stCamData.nMasterIndex > 0) OR
         NOT (_stCamData.nMasterIndex < PLC_CONSTANT.MAX_AXIS+1)
      THEN
        _eState                     := E_PROGRESS.ERROR;
        _eCk                        := E_CHECK_STATE.CAM_MASTER_AXIS_REF_INVALID;

        _stMsg.eType                := E_MessageType.eMessageError;
        _stMsg.eDevice              := E_Device.Camming + TO_DINT(_AxisSlave.NcToPlc.AxisId);
        _stMsg.eSubdevice           := e_Subdevice.CamControl;
        _stMsg.iErrorNumber         := _stCamData.nMasterIndex;
  
        _stMsg.sText                := concat('CAM_IN: ', TO_STRING(_eCk));
        f_MessageSet(_stMsg);

      ELSE
        _eState                     := E_PROGRESS.BUSY;
        _eCk                        := E_CHECK_STATE.CHECK_DONE;
        _eResult                    := CamIn(FALSE, _stCamData.nMasterIndex);
      END_IF
    END_IF
END_CASE
CASE _eState
OF
  E_PROGRESS.BUSY:
      _eResult                      := CamIn(TRUE, _stCamData.nMasterIndex);

    IF (_eResult = E_PROGRESS.ERROR)
    THEN
      CamIn(FALSE, _stCamData.nMasterIndex);
      _eState                       := E_PROGRESS.ERROR;
      _udiErr                       := _fbCamInV2.ErrorID;

    ELSIF (_eResult = E_PROGRESS.DONE)
    THEN
      CamIn(FALSE, _stCamData.nMasterIndex);
      _eState                       := E_PROGRESS.DONE;
    END_IF
END_CASE


AxisCamIn                           := _eState;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="AxisCtrl" Id="{93d3e953-2124-054d-1d4b-976830aa1d1e}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY AxisCtrl : pointer to ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF st_axis_ctrl]]></Declaration>
      <Set Name="Set" Id="{ff57e0b9-0068-0d41-37db-940060184044}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Ctrl := AxisCtrl;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="AxisData" Id="{9d57ddb0-b760-054c-0524-784853cb09b9}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY AxisData : pointer to ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF ST_CAM_DATA]]></Declaration>
      <Set Name="Set" Id="{ac84c55f-38f7-0ae7-0c49-69e09a93af06}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Data := AxisData;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="AxisIndex" Id="{70baf2e8-ce16-0817-2b3d-8567adcb5f2c}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY AxisIndex : uint]]></Declaration>
      <Set Name="Set" Id="{e9025d8b-e67a-0c0e-2bd4-7fabc385ed10}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="AxisInfo" Id="{de6dfee5-738d-0b13-0ede-3781128cca0c}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY AxisInfo : pointer to ARRAY[1..PLC_CONSTANT.MAX_AXIS] OF ST_AXIS_INFO]]></Declaration>
      <Set Name="Set" Id="{50a9dbcf-691d-08c7-33a8-8387b122475b}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Info := AxisInfo;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="CamInit" Id="{a1cd52de-82fd-08e9-19f2-ceb0d650d7cc}" FolderPath="methods\private\">
      <Declaration><![CDATA[METHOD PRIVATE CamInit : E_PROGRESS
VAR_INPUT
  Execute           : BOOL;
END_VAR
VAR_INST
  _eResult,
  _eState           : E_PROGRESS;
  _rValue           : LREAL;
  _stMsg            : ST_Message;

  // access to CoE register
  _sDeviceName      : STRING(256) := 'NO NAME';
  _fbCoeRead				: Tc2_MC2_Drive.FB_CoERead;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT Check() THEN _eState := E_PROGRESS.ERROR; END_IF
IF NOT Execute THEN _eState := E_PROGRESS.INIT; END_IF

CASE _eState
OF
  E_PROGRESS.INIT:
    IF Execute 
    THEN
      memset(ADR(_stMsg),0,SIZEOF(_stMsg));

      _nErrorId                     := 0;
      _rValue                       := 0;
      _eState                       := E_PROGRESS.BUSY;
      _eInit                        := _eState;
    END_IF
END_CASE
CASE _eState
OF
  E_PROGRESS.BUSY:
    _eState                         := E_PROGRESS.STARTUP;
END_CASE
CASE _eState
OF
  E_PROGRESS.STARTUP:
    // keep this for future use
    _eResult                        := E_PROGRESS.DONE;//StartUp(TRUE);
    IF (_eResult = E_PROGRESS.DONE)
    THEN
      _eState                       := E_PROGRESS.PREPARE;
      _ItfMcAxis[_nAxisIndex].Reset(FALSE);
    ELSIF (_eResult = E_PROGRESS.ERROR)
    THEN
      _eState                       := _eResult;

      _stMsg.eType                  := E_MessageType.eMessageError;
      _stMsg.eDevice                := e_Device.Camming + TO_DINT(_AxisSlave.NcToPlc.AxisId);
      _stMsg.eSubdevice             := e_Subdevice.fbAxis_StartUp;
      _stMsg.iErrorNumber := _nErrorId  := 6666;

      _stMsg.sText                  := TO_STRING(_eState);
      f_MessageSet(_stMsg);
    END_IF
END_CASE
CASE _eState
OF
  E_PROGRESS.PREPARE:
    _eResult                        := _ItfMcAxis[_nAxisIndex].Reset(TRUE);
    IF (_eResult = E_PROGRESS.DONE)
    THEN
      _eState                       := E_PROGRESS.WORKING;
    ELSIF (_eResult = E_PROGRESS.ERROR)
    THEN
      _eState                       := _eResult;

      _stMsg.eType                  := E_MessageType.eMessageError;
      _stMsg.eDevice                := e_Device.Axis + TO_DINT(_AxisSlave.NcToPlc.AxisId);
      _stMsg.eSubdevice             := e_Subdevice.fbAxis_StartupReset;
      _stMsg.iErrorNumber           := _AxisSlave.Status.ErrorID;

      _stMsg.sText                  := TO_STRING(_eState);
      f_MessageSet(_stMsg);
    END_IF
END_CASE
CASE _eState
OF
  E_PROGRESS.READY:
    _eResult                        := _ItfMcAxis[_nAxisIndex].Reset(FALSE);
    _eState                         := E_PROGRESS.WAITING;
END_CASE
CASE _eState
OF
  E_PROGRESS.WAITING:
    _eState                         := E_PROGRESS.WORKING;
END_CASE
CASE _eState
OF
  E_PROGRESS.WORKING:
    _eState                         := E_PROGRESS.DONE;

    _stMsg.eType                    := E_MessageType.eMessageInfo;
    _stMsg.eDevice                  := e_Device.Camming + TO_DINT(_AxisSlave.NcToPlc.AxisId);
    _stMsg.eSubdevice               := e_Subdevice.fbCamAxis;
    _stMsg.iErrorNumber             := 0;
    _stMsg.sText                    := concat('Camming Init: ', TO_STRING(_eState));

    f_MessageSet(_stMsg);
END_CASE

CamInit                            := _eState;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Check" Id="{0e445eb0-1468-0be5-26fa-f4aca0306678}" FolderPath="methods\private\">
      <Declaration><![CDATA[METHOD PRIVATE Check : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
//-----------------------------------------------------------------------------
IF (_nAxisIndex < 1) OR
   (_nAxisIndex > PLC_CONSTANT.MAX_AXIS)
THEN
  _eCheck                 := E_CHECK_STATE.AXIS_INDEX_OUT_OF_RANGE;
  RETURN;
END_IF
//-----------------------------------------------------------------------------
// get my AXIS_REF pointer for use in base class
//-----------------------------------------------------------------------------
_AxisSlave                := _Axes[_nAxisIndex];
//-----------------------------------------------------------------------------

IF NOT (_ItfMcAxis[_nAxisIndex] <> 0)
THEN
  _eCheck                 := E_CHECK_STATE.CAM_SLAVE_ITF_MC_AXIS_INVALID;
  RETURN;
END_IF


IF NOT (_Ctrl <> 0)
THEN
  _eCheck                 := E_CHECK_STATE.AXIS_CTRL_POINTER_INVALID;
  RETURN;
END_IF

IF NOT (_State <> 0)
THEN
  _eCheck                 := E_CHECK_STATE.AXIS_STATE_POINTER_INVALID;
  RETURN;
END_IF

IF NOT (_Info <> 0)
THEN
  _eCheck                 := E_CHECK_STATE.AXIS_INFO_POINTER_INVALID;
  RETURN;
END_IF

IF NOT (_Data <> 0)
THEN
  _eCheck                 := E_CHECK_STATE.AXIS_DATA_POINTER_INVALID;
  RETURN;
END_IF

//-----------------------------------------------------------------------------
//  NaN INF check
//-----------------------------------------------------------------------------
IF NOT IsFinite(F_LReal(_Data^[_nAxisIndex].rActivationPosition))
THEN
  _Data^[_nAxisIndex].rActivationPosition               := 0.0;
END_IF

IF NOT IsFinite(F_LReal(_Data^[_nAxisIndex].stCamScalingData.rMasterOffset))
THEN
  _Data^[_nAxisIndex].stCamScalingData.rMasterOffset    := 0.0;
END_IF

IF NOT IsFinite(F_LReal(_Data^[_nAxisIndex].stCamScalingData.rMasterScaling))
THEN
  _Data^[_nAxisIndex].stCamScalingData.rMasterScaling   := 0.0;
END_IF

IF NOT IsFinite(F_LReal(_Data^[_nAxisIndex].stCamScalingData.rSlaveOffset))
THEN
  _Data^[_nAxisIndex].stCamScalingData.rSlaveOffset     := 0.0;
END_IF

IF NOT IsFinite(F_LReal(_Data^[_nAxisIndex].stCamScalingData.rSlaveScaling))
THEN
	_Data^[_nAxisIndex].stCamScalingData.rSlaveScaling   := 0.0;
END_IF
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
_eCheck                   := E_CHECK_STATE.CHECK_DONE;
Check                     := TRUE;
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Cmd" Id="{20158710-5918-0ddf-2399-0295e7f0b9c3}" FolderPath="methods\private\">
      <Declaration><![CDATA[//-------------------------------------------------------------------------------------
//
//  HAUD 2024 01 20
//
//  Cmd
//    - gets state for ctrl
//    - checks for eInit to be done, before any other commands are accepted
//    - prepare your commands here
//    - init what you have to here
//
//-------------------------------------------------------------------------------------
METHOD PRIVATE Cmd  : E_AXIS_STATE
VAR_INPUT
  eCmd              : E_AXIS_CTRL;
END_VAR
VAR_INST
  _IdxMaster        : UINT;
  _estate           : E_AXIS_STATE;
  _eres             : E_PROGRESS;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE eCmd
OF
  //-------------------------------------------------------------------------------------
  E_AXIS_CTRL.CAM_INIT:
    _eInit                            := CamInit(FALSE);
    _estate                           := E_AXIS_STATE.CAM_INIT;
ELSE
  IF (_eInit <> E_PROGRESS.DONE)
  THEN
    _estate                           := E_AXIS_STATE.AXIS_NO_INIT;
  ELSE
    CASE eCmd
    OF
      //---------------------------------------------------------------------------------
      E_AXIS_CTRL.CAM_IN:
        _IdxMaster                    := _Data^[_nAxisIndex].nMasterIndex;
        _stCamData                    := _Data^[_nAxisIndex];
        _eres                         := AxisCamIn(FALSE);
        _estate                       := E_AXIS_STATE.CAM_IN;

      //---------------------------------------------------------------------------------
      E_AXIS_CTRL.CAM_SCALING:
        _estate                       := E_AXIS_STATE.CAM_SCALING;
      //---------------------------------------------------------------------------------
      E_AXIS_CTRL.CAM_INFO:
        _estate                       := E_AXIS_STATE.CAM_INFO;

      //---------------------------------------------------------------------------------
      E_AXIS_CTRL.CAM_RD_MOTION_FUNCTION:
        _estate                       := E_AXIS_STATE.CAM_RD_MOTION_FUNCTION;

      //---------------------------------------------------------------------------------
      E_AXIS_CTRL.CAM_RD_MOTION_FUNCTION_POINT:
        _estate                       := E_AXIS_STATE.CAM_RD_MOTION_FUNCTION_POINT;

      //---------------------------------------------------------------------------------
      E_AXIS_CTRL.CAM_RD_MOTION_FUNCTIONS_VALUES:
        _estate                       := E_AXIS_STATE.CAM_RD_MOTION_FUNCTIONS_VALUES;

      //---------------------------------------------------------------------------------
      E_AXIS_CTRL.CAM_WRT_MOTION_FUNCTION:
        _estate                       := E_AXIS_STATE.CAM_WRT_MOTION_FUNCTION;

      //---------------------------------------------------------------------------------
      E_AXIS_CTRL.CAM_WRT_MOTION_FUNCTION_POINT:
        _estate                       := E_AXIS_STATE.CAM_WRT_MOTION_FUNCTION_POINT;

    //-----------------------------------------------------------------------------------
    ELSE
      _estate                         := eCmd;
      _eres                           := E_PROGRESS.ERROR; // wrong note! wrong note!
    END_CASE
  END_IF // _eInit
END_CASE

IF NOT (_eres = E_PROGRESS.ERROR)
THEN
  // only get busy on valid command
  _State^[_nAxisIndex].eState       := _eState + E_PROGRESS.BUSY;
END_IF

Cmd                                   := _estate;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Cycle" Id="{594645a2-c412-0211-2ac3-4cf321f1c079}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD Cycle  : e_progress
VAR_INST
  _eCycle     : E_PROGRESS;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT Check() THEN Cycle := _eCycle := _eResult := E_PROGRESS.ERROR; RETURN; END_IF

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// get command from outside world
//-----------------------------------------------------------------------------
_eCtrl                              := _Ctrl^[_nAxisIndex].eCtrl;
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
//  OnChange react to command
//-----------------------------------------------------------------------------
IF (_eCtrl <> _eCtrlOld)
THEN
  // get busy
  _eState                           := Cmd(_eCtrl); // get execution state for command
  _eCtrlOld                         := _eCtrl;
  LogControl();
END_IF
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
//  execution state for command
//-----------------------------------------------------------------------------
CASE _eState
OF
  E_AXIS_STATE.AXIS_INIT:
    //-------------------------------------------------------------------------
    // axis always requires one init from outside world at startup
    // _eInit has to be valid for other commands to be accepted in Cmd(_eCtrl)
    _eInit := _eResult              := CamInit(TRUE);

  E_AXIS_STATE.AXIS_NO_INIT:
    //-------------------------------------------------------------------------
    // we are here because you sent a command before having done AXIS_INIT
    _eResult                        := E_PROGRESS.ERROR;

  E_AXIS_STATE.AXIS_IDLE:
    _eResult                        := AxisCamIdle(TRUE);

ELSE
  IF (_eCtrl > E_AXIS_CTRL.AXIS_NULL)
  THEN
    _eResult                          := E_PROGRESS.NOT_EXIST;
    _Info^[_nAxisIndex].bError        := TRUE;
  ELSE
    _eResult                          := E_PROGRESS.INVALID;
  END_IF
END_CASE
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
//  OnChange of execution state result
//    - log result and clear error number on interface
//-----------------------------------------------------------------------------
IF (_eResult <> _eResultOld)
THEN
  LogResult();
  _eResultOld                       := _eResult;
END_IF
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
]]></ST>
      </Implementation>
    </Method>
    <Method Name="LogControl" Id="{4f22ed13-a65b-05b5-1bf2-856e5cab8a84}" FolderPath="methods\private\">
      <Declaration><![CDATA[METHOD PRIVATE LogControl
VAR_INST
  _sResult,
  _sCtrl            : STRING(30);

  _stMsg            : ST_Message;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[  _stMsg.eType               := E_MessageType.eMessageInfo;
  _stMsg.eDevice             := E_Device.Camming + TO_DINT(_AxisSlave.NcToPlc.AxisId);
  _stMsg.eSubdevice          := e_Subdevice.fbCamAxis;
  _stMsg.iErrorNumber        := _nAxisIndex;

  _sCtrl                     := concat('E_AXIS_CTRL', '.');
  _sCtrl                     := concat(_sCtrl, TO_STRING(_eCtrl));

  _stMsg.sText               := concat('new command: ', _sCtrl);
  f_MessageSet(_stMsg);

]]></ST>
      </Implementation>
    </Method>
    <Method Name="LogResult" Id="{b39198d6-ccc4-0c60-0d62-f21eed01d3f8}" FolderPath="methods\private\">
      <Declaration><![CDATA[METHOD PRIVATE LogResult
VAR_INST
  _sResult,
  _sCtrl            : STRING(30);

  _stMsg            : ST_Message;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
  _stMsg.eType               := E_MessageType.eMessageInfo;
  _stMsg.eDevice             := E_Device.Camming + TO_DINT(_AxisSlave.NcToPlc.AxisId);
  _stMsg.eSubdevice          := e_Subdevice.fbCamAxis;
  _stMsg.iErrorNumber        := _nAxisIndex;

  _sCtrl                     := concat('E_AXIS_CTRL', '.');
  _sCtrl                     := concat(_sCtrl, TO_STRING(_eCtrl));

  _sResult                   := concat('E_PROGRESS', '.');
  _sResult                   := concat(_sResult, TO_STRING(_eResult));

  _stMsg.sText               := concat(_sCtrl, ' ');
  _stMsg.sText               := concat(_sCtrl, _sResult);
  f_MessageSet(_stMsg);

]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_CamCtrl">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.Axes.Set">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.AxisCamIdle">
      <LineId Id="824" Count="82" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.AxisCamIn">
      <LineId Id="1094" Count="0" />
      <LineId Id="983" Count="0" />
      <LineId Id="890" Count="7" />
      <LineId Id="900" Count="0" />
      <LineId Id="1038" Count="0" />
      <LineId Id="1003" Count="2" />
      <LineId Id="1007" Count="0" />
      <LineId Id="1022" Count="0" />
      <LineId Id="1029" Count="7" />
      <LineId Id="1023" Count="0" />
      <LineId Id="1009" Count="0" />
      <LineId Id="1021" Count="0" />
      <LineId Id="1010" Count="0" />
      <LineId Id="1027" Count="0" />
      <LineId Id="1008" Count="0" />
      <LineId Id="918" Count="4" />
      <LineId Id="930" Count="0" />
      <LineId Id="1082" Count="0" />
      <LineId Id="931" Count="1" />
      <LineId Id="1091" Count="0" />
      <LineId Id="933" Count="1" />
      <LineId Id="1083" Count="0" />
      <LineId Id="948" Count="1" />
      <LineId Id="1092" Count="0" />
      <LineId Id="950" Count="0" />
      <LineId Id="964" Count="0" />
      <LineId Id="1086" Count="0" />
      <LineId Id="1093" Count="0" />
      <LineId Id="966" Count="1" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.AxisCtrl.Set">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.AxisData.Set">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.AxisIndex.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.AxisInfo.Set">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.CamInit">
      <LineId Id="289" Count="88" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.Check">
      <LineId Id="164" Count="0" />
      <LineId Id="167" Count="6" />
      <LineId Id="175" Count="5" />
      <LineId Id="174" Count="0" />
      <LineId Id="181" Count="3" />
      <LineId Id="166" Count="0" />
      <LineId Id="11" Count="5" />
      <LineId Id="18" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="19" Count="3" />
      <LineId Id="126" Count="5" />
      <LineId Id="38" Count="0" />
      <LineId Id="55" Count="5" />
      <LineId Id="96" Count="0" />
      <LineId Id="98" Count="1" />
      <LineId Id="101" Count="23" />
      <LineId Id="161" Count="0" />
      <LineId Id="95" Count="0" />
      <LineId Id="9" Count="1" />
      <LineId Id="153" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="162" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.Cmd">
      <LineId Id="387" Count="62" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.Cycle">
      <LineId Id="10" Count="0" />
      <LineId Id="81" Count="0" />
      <LineId Id="24" Count="56" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.LogControl">
      <LineId Id="35" Count="10" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_CamCtrl.LogResult">
      <LineId Id="7" Count="0" />
      <LineId Id="9" Count="3" />
      <LineId Id="22" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="33" Count="1" />
      <LineId Id="20" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="15" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>