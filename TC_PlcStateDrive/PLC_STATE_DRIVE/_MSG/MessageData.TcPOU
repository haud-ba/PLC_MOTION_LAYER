<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MessageData" Id="{cbf579ba-877c-0512-0a45-14c3e31a789c}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MessageData
VAR_INPUT
  bActivate     : BOOL;   // activates cyclic copy of message list to visualization; switched on in T_MESSAGE_LIST visu
	bPowerFail		: BOOL;   // UPS switch
	bWriteFile		: BOOL;   // manual write of list
	bReset				: BOOL;
	pList					: POINTER TO ARRAY[1..PLC_CONSTANT.MAX_MESSAGE] OF ST_Message; // attach your list here
	pLocalTime		: POINTER TO DT; 
END_VAR
VAR_OUTPUT
	bBusy					: BOOL;
  bDone         : BOOL;
	VisuList		  : ARRAY[1..PLC_CONSTANT.MAX_MESSAGE] OF ST_Message;
END_VAR
VAR
	eState 				: E_MESSAGE_STATE;

	rtrigReset		: Tc2_Standard.R_TRIG;
	aWriteList		: ARRAY[1..PLC_CONSTANT.MAX_MESSAGE] OF ST_Message;
	sWriteData		: STRING(32767);
	sNewLine			: STRING := '$N';
	sTab					: STRING := '$T';
	iCount, 
	iStart, 
	iEnd					: INT;

	fbCreateDir		: Tc2_System.FB_CreateDir;
	sTmp          : STRING(256);
	sFilename			: STRING(256);
	sFolderDate		: STRING;
	bFileCreated	: BOOL;

	fbFileOpen		  : Tc2_System.FB_FileOpen;
	fbFileWrite		  : Tc2_System.FB_FileWrite;
	fbFileClose		  : Tc2_System.FB_FileClose;

  ivis          : INT;

  bBusyWrite,
  bErrorWrite   : BOOL;
  nErrorWrite   : UDINT;
  nStateWrite   : UINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//-----------------------------------------------------------------------------
// check pointers
//-----------------------------------------------------------------------------
IF NOT (pList <> 0) OR
   NOT (pLocalTime <> 0)
THEN
  eState                      := MSG_INVALID_POINTERS;
  RETURN;
END_IF
//-----------------------------------------------------------------------------
// check date
// new message file for a new day
//-----------------------------------------------------------------------------
IF (FolderDate(pLocalTime) <> sFolderDate)
THEN
  bFileCreated                := FALSE;
END_IF
//-----------------------------------------------------------------------------
//----------------------------------------------------------------------------- 
rtrigReset(CLK:= bReset);
IF rtrigReset.Q
THEN
  bReset                      := FALSE;
  eState                      := MSG_NULL;
END_IF
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_NULL:
    bBusy                     := 
    bDone                     := FALSE;
    
    IF (bWriteFile) OR
       (bPowerFail) OR 
       (GVL_MSG.MessageIndex > PLC_CONSTANT.MAX_MESSAGE-1)
    THEN
      eState                  := MSG_INIT;
    END_IF
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_INIT:
    WriteData(FALSE);

    // clear intern list
    memset(ADR(aWriteList),     0, SIZEOF(aWriteList));

    // copy from attached list to local
    memcpy(ADR(aWriteList), pList, SIZEOF(aWriteList));

    // clear attached list
    memset(pList, 0, SIZEOF(aWriteList));

    bBusy                     := TRUE;
    iStart 	                  := 1;
    iEnd 		                  := SEL((GVL_MSG.MessageIndex > PLC_CONSTANT.MAX_MESSAGE-1),
                                      GVL_MSG.MessageIndex,
                                      PLC_CONSTANT.MAX_MESSAGE);
    GVL_MSG.MessageIndex      := 1;

    IF NOT bFileCreated
    THEN
      IF (GVL_MSG.MessageError > 0) OR
         (GVL_MSG.EnableLogging)
      THEN
        // folder and filename have to be created
        eState                := MSG_ROOT;
      ELSE
        eState                := MSG_WRITE_FILE_DONE; 
      END_IF
    ELSE
      // folder and file already exist
      eState                  := MSG_LINE_START;
    END_IF
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_ROOT:
    eState                    := MSG_ROOT_CREATE;
    fbCreateDir.sPathName     := left(PLC_CONSTANT.LOG_DIR,len(PLC_CONSTANT.LOG_DIR)-1);
    fbCreateDir(
            bExecute	        := FALSE);
  
  MSG_ROOT_CREATE:
    (* create root folder *)
    fbCreateDir(
            bExecute	        := TRUE);
    IF fbCreateDir.bBusy
    THEN
      eState                  := MSG_ROOT_CREATE_BUSY;
    END_IF
  
  MSG_ROOT_CREATE_BUSY:
    fbCreateDir(
            bExecute	        := TRUE);
    IF NOT fbCreateDir.bBusy
    THEN
      eState                  := MSG_FOLDER;
    END_IF
  
  MSG_FOLDER:
    eState                    := MSG_FOLDER_CREATE;
    sFolderDate               := FolderDate(pLocalTime);
    fbCreateDir.sPathName     := concat(PLC_CONSTANT.LOG_DIR, sFolderDate);
    fbCreateDir(
            bExecute	        := FALSE);

  MSG_FOLDER_CREATE:
    (* create active folder *)
    fbCreateDir(
            bExecute	        := TRUE);
    IF fbCreateDir.bBusy
    THEN
      eState                  := MSG_FOLDER_CREATE_BUSY;
    END_IF
  
  MSG_FOLDER_CREATE_BUSY:
    fbCreateDir(
            bExecute	        := TRUE);
    IF NOT fbCreateDir.bBusy
    THEN
      eState                  := MSG_FILE_CREATE_DONE;
      fbCreateDir(
              bExecute	      := FALSE);
    END_IF
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_FILE_CREATE_DONE:
    eState                    := MSG_LINE_START;
    bFileCreated              := TRUE;
    // build complete file path
    sTmp                      := concat(concat(PLC_CONSTANT.LOG_FILE, sFolderDate),'.txt');
    sTmp                      := concat('\', sTmp);
    sFilename                 := concat(fbCreateDir.sPathName, sTmp);
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_LINE_START:
    GVL_MSG.MessageError      := 0;
    eState                    := MSG_LINE_BUILD;
    iCount                    := iStart;
    memset(ADR(sWriteData),0,SIZEOF(sWriteData));
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_LINE_BUILD:
    eState                    := MSG_LINE_CHECK;
    IF (aWriteList[iCount].eType > E_MessageType.eMessageEmpty)
    THEN
      IF (aWriteList[iCount].eType > E_MessageType.eMessageInfo) OR
         (GVL_MSG.EnableLogging)
      THEN
        sTmp                  := TO_STRING(aWriteList[iCount].eType);
        sTmp                  := Tc2_Standard.CONCAT(sTmp, sTab);
        sTmp                  := Tc2_Standard.CONCAT(sTmp, aWriteList[iCount].tTime);
        sTmp                  := Tc2_Standard.CONCAT(sTmp, sTab);
        sTmp                  := Tc2_Standard.CONCAT(sTmp, Device_To_String(aWriteList[iCount].eDevice));
        sTmp                  := Tc2_Standard.CONCAT(sTmp, sTab);
        sTmp                  := Tc2_Standard.CONCAT(sTmp, TO_STRING(aWriteList[iCount].eSubdevice));
        sTmp                  := Tc2_Standard.CONCAT(sTmp, sTab);
        sTmp                  := Tc2_Standard.CONCAT(sTmp, TO_STRING(aWriteList[iCount].iErrorNumber));
        sTmp                  := Tc2_Standard.CONCAT(sTmp, sTab);
        sTmp                  := Tc2_Standard.CONCAT(sTmp, aWriteList[iCount].sText);
        sTmp                  := Tc2_Standard.CONCAT(sTmp, sNewLine);
        Builder(ADR(sTmp), ADR(sWriteData), StringLen(ADR(sTmp)));
      END_IF
    END_IF
    iCount                    := iCount + 1;
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_LINE_CHECK:
  IF NOT (iCount > iEnd)
  THEN
    eState                    := MSG_LINE_BUILD;
  ELSE
    IF (StringLen(ADR(sWriteData)) > 1)
    THEN
      eState                  := MSG_WRITE_FILE_BUSY;
    ELSE
      eState                  := MSG_WRITE_FILE_DONE;
    END_IF
  END_IF
  
  MSG_WRITE_FILE_BUSY:
  IF WriteData(TRUE)
  THEN
    eState                    := MSG_WRITE_FILE_END;
  END_IF
  
  MSG_WRITE_FILE_END:
  WriteData(FALSE);
  eState                      := MSG_WRITE_FILE_DONE;
  
  MSG_WRITE_FILE_DONE:
  bDone                       := TRUE;
  IF NOT (bWriteFile)
  THEN
    eState                    := MSG_NULL;
  END_IF
END_CASE
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// visu message list
//-----------------------------------------------------------------------------
memset(ADR(VisuList),0,SIZEOF(VisuList));

IF (bActivate)
THEN
  // copy message list to visu message list
  FOR ivis := 1 TO GVL_MSG.MessageIndex
  DO
    VisuList[ivis]            := pList^[ivis];
  END_FOR
END_IF
//-----------------------------------------------------------------------------
]]></ST>
    </Implementation>
    <Method Name="Builder" Id="{e7f6a64d-5362-0a59-3bd9-ff631826ba13}">
      <Declaration><![CDATA[METHOD Builder : BOOL // string concat for length > 255
VAR_INPUT
  pInString   : POINTER TO STRING;
  pOutString  : POINTER TO STRING(32767);
  Length      : UINT;
END_VAR
VAR
  Counter     : DINT;
  pInWork     : POINTER TO BYTE;
  pOutWork    : POINTER TO BYTE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pOutWork := pOutString;
pInWork := pInString;
Counter := 0;

(*check termination*)
WHILE (pOutWork^ <> 0) AND
      (Counter < 32767)
DO
  Counter := Counter + 1;
  pOutWork := pOutWork + 1;
END_WHILE

(* append only if data fits into output *)
IF (Counter + Length < 32767)
THEN

  (* append data *)
  FOR Counter := 1 TO Length
  DO
    pOutWork^ := pInWork^;
    pOutWork := pOutWork + 1;
    pInWork := pInWork + 1;
  END_FOR

  (* terminate *)
  pOutWork := pOutWork + 1;
  pOutWork^ := 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Device_to_String" Id="{a4fca8d1-7396-0a84-395c-ec256a43027b}">
      <Declaration><![CDATA[METHOD Device_to_String : string
VAR_INPUT
  eMsgDevice    : e_Device;
END_VAR
VAR
  // first NC Axis, all other Axis are incremented
  nmodAxis            : DINT := 1000;

  // first NCI channel control instance
  nmodNciChannelCtrl  : DINT := 2000;

  // NCI channel
  nmodNciChannel      : DINT := 3000;

  // CAMMING
  nmodCamming         : DINT := 6000;

  // message function blocks
  nmodData            : DINT := 10000;

  sId                 : STRING := '_ID_';
  nId                 : DINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF (eMsgDevice <= nmodAxis)
THEN
  // general case with no added indices
  Device_to_String := TO_STRING(eMsgDevice);
ELSE

  IF (eMsgDevice > nModData)
  THEN
    nId := eMsgDevice MOD nmodData;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Data), sId);

  ELSIF (eMsgDevice > nmodCamming)
  THEN
    nId := eMsgDevice MOD nmodCamming;
    sId := concat(sId, TO_STRING(nId));
    sId := concat('_AXIS', sId);
 
    Device_to_String := concat(TO_STRING(e_device.Camming), sId);

  ELSIF (eMsgDevice > nmodNciChannel)
  THEN
    nId := eMsgDevice MOD nmodNciChannel;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.NciChannel), sId);

  ELSIF (eMsgDevice > nmodNciChannelCtrl)
  THEN
    nId := eMsgDevice MOD nmodNciChannelCtrl;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.NciChannelCtrl), sId);

  ELSIF (eMsgDevice > nmodAxis)
  THEN
    nId := eMsgDevice MOD nmodAxis;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Axis), sId);
  ELSE
    Device_to_String := TO_STRING(eMsgDevice);
  END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FolderDate" Id="{769271c2-f3d0-0e78-1191-f90d8d6bf2eb}">
      <Declaration><![CDATA[METHOD FolderDate : STRING
VAR_INPUT
	pLocalTime		: POINTER TO DT;
END_VAR
VAR
	sTmp					: STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT (pLocalTime <> 0)
THEN
	FolderDate := 'NULL';
	RETURN;
END_IF

sTmp := DT_TO_STRING(pLocalTime^);

// cut off #dt literal
sTmp := right(sTmp, len(sTmp)-3);

sTmp := replace(sTmp, '_', 1, 5);
sTmp := replace(sTmp, '_', 1, 8);

sTmp := left(sTmp,10);	// year_month_day

FolderDate := sTmp;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="StringLen" Id="{4b37d558-187c-07d9-2288-9af673b19b4e}">
      <Declaration><![CDATA[METHOD StringLen : UINT // 	string length for len > 255
VAR_INPUT
  pInString		: POINTER TO STRING(32767);
END_VAR
VAR
	Counter			: UINT;
	pInWork			: POINTER TO BYTE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pInWork := pInString;
Counter := 0;

WHILE (pInWork^ <> 0) AND
			(Counter < 32767)
DO
	Counter := Counter + 1;
	pInWork := pInWork + 1;
END_WHILE

StringLen := Counter;]]></ST>
      </Implementation>
    </Method>
    <Method Name="WriteData" Id="{fc730c43-20dd-03f0-168b-c4b918814d9f}">
      <Declaration><![CDATA[METHOD WriteData  : BOOL
VAR_INPUT
  bExecute        : BOOL;
END_VAR
VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT bExecute
THEN
  bBusyWrite                := FALSE;
	nStateWrite               := 0;
END_IF

CASE nStateWrite OF

	0:
		fbFileOpen.bExecute		  := FALSE;
		fbFileWrite.bExecute	  := FALSE;
		fbFileClose.bExecute	  := FALSE;

		IF bExecute
		THEN
			bBusyWrite            := TRUE;
			nStateWrite           := 10;
		END_IF

	10:
		fbFileOpen.bExecute     := TRUE;
		IF fbFileOpen.bBusy
		THEN
			nStateWrite           := 20;
		END_IF

	20:
		IF NOT fbFileOpen.bBusy
		THEN
			fbFileOpen.bExecute   := FALSE;
			fbFileWrite.bExecute  := TRUE;
			nStateWrite           := 30;
		END_IF

	30:
		IF fbFileWrite.bBusy
		THEN
			nStateWrite           := 40;
		END_IF

	40:
		IF NOT fbFileWrite.bBusy
		THEN
			fbFileWrite.bExecute  := FALSE;
			fbFileClose.bExecute  := TRUE;
			nStateWrite           := 50;
		END_IF

	50:
		IF fbFileClose.bBusy
		THEN
			nStateWrite           := 60;
		END_IF

	60:
		IF NOT fbFileClose.bBusy
		THEN
			fbFileClose.bExecute  := FALSE;
			nStateWrite 		      := 100;
		END_IF
	
	100:
    WriteData               := TRUE;
END_CASE


fbFileOpen(
	sNetId:= , 
	sPathName:= sFileName, 
	nMode:= FOPEN_MODEAPPEND, 
	ePath:= PATH_GENERIC, 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> , 
	hFile=> );

fbFileWrite(
	sNetId:= , 
	hFile:= fbFileOpen.hFile, 
	pWriteBuff:= ADR(sWriteData), 
	cbWriteLen:= StringLen(ADR(sWriteData)), 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> nErrorWrite, 
	cbWrite=> );

fbFileClose(
	sNetId:= , 
	hFile:= fbFileOpen.hFile, 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> );

bErrorWrite := fbFileOpen.bError OR fbFileWrite.bError;
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="MessageData">
      <LineId Id="3663" Count="0" />
      <LineId Id="3460" Count="0" />
      <LineId Id="3664" Count="0" />
      <LineId Id="3461" Count="2" />
      <LineId Id="3676" Count="0" />
      <LineId Id="3464" Count="1" />
      <LineId Id="3660" Count="0" />
      <LineId Id="3466" Count="0" />
      <LineId Id="3661" Count="0" />
      <LineId Id="3665" Count="0" />
      <LineId Id="3467" Count="3" />
      <LineId Id="3472" Count="0" />
      <LineId Id="3662" Count="0" />
      <LineId Id="3473" Count="6" />
      <LineId Id="3666" Count="0" />
      <LineId Id="3481" Count="11" />
      <LineId Id="3667" Count="0" />
      <LineId Id="3493" Count="2" />
      <LineId Id="3688" Count="0" />
      <LineId Id="3496" Count="1" />
      <LineId Id="3689" Count="0" />
      <LineId Id="3687" Count="0" />
      <LineId Id="3498" Count="0" />
      <LineId Id="3691" Count="0" />
      <LineId Id="3690" Count="0" />
      <LineId Id="3499" Count="10" />
      <LineId Id="3678" Count="0" />
      <LineId Id="3684" Count="0" />
      <LineId Id="3679" Count="0" />
      <LineId Id="3658" Count="0" />
      <LineId Id="3510" Count="0" />
      <LineId Id="3682" Count="1" />
      <LineId Id="3680" Count="0" />
      <LineId Id="3511" Count="0" />
      <LineId Id="3659" Count="0" />
      <LineId Id="3512" Count="2" />
      <LineId Id="3668" Count="0" />
      <LineId Id="3515" Count="21" />
      <LineId Id="3541" Count="2" />
      <LineId Id="3646" Count="0" />
      <LineId Id="3642" Count="3" />
      <LineId Id="3647" Count="0" />
      <LineId Id="3641" Count="0" />
      <LineId Id="3544" Count="13" />
      <LineId Id="3563" Count="2" />
      <LineId Id="3649" Count="0" />
      <LineId Id="3669" Count="0" />
      <LineId Id="3650" Count="1" />
      <LineId Id="3657" Count="0" />
      <LineId Id="3652" Count="4" />
      <LineId Id="3566" Count="0" />
      <LineId Id="3670" Count="0" />
      <LineId Id="3567" Count="1" />
      <LineId Id="3686" Count="0" />
      <LineId Id="3569" Count="3" />
      <LineId Id="3671" Count="0" />
      <LineId Id="3573" Count="24" />
      <LineId Id="3672" Count="0" />
      <LineId Id="3598" Count="32" />
      <LineId Id="3673" Count="1" />
      <LineId Id="3631" Count="9" />
      <LineId Id="5" Count="0" />
      <LineId Id="3675" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.Builder">
      <LineId Id="16" Count="27" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.Device_to_String">
      <LineId Id="24" Count="2" />
      <LineId Id="30" Count="0" />
      <LineId Id="27" Count="1" />
      <LineId Id="34" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="35" Count="1" />
      <LineId Id="39" Count="1" />
      <LineId Id="38" Count="0" />
      <LineId Id="88" Count="4" />
      <LineId Id="95" Count="0" />
      <LineId Id="93" Count="1" />
      <LineId Id="42" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="43" Count="0" />
      <LineId Id="46" Count="3" />
      <LineId Id="44" Count="0" />
      <LineId Id="50" Count="5" />
      <LineId Id="45" Count="0" />
      <LineId Id="56" Count="7" />
      <LineId Id="33" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.FolderDate">
      <LineId Id="10" Count="12" />
      <LineId Id="25" Count="1" />
      <LineId Id="28" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.StringLen">
      <LineId Id="12" Count="9" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.WriteData">
      <LineId Id="11" Count="2" />
      <LineId Id="124" Count="0" />
      <LineId Id="14" Count="8" />
      <LineId Id="123" Count="0" />
      <LineId Id="25" Count="1" />
      <LineId Id="28" Count="42" />
      <LineId Id="72" Count="3" />
      <LineId Id="122" Count="0" />
      <LineId Id="78" Count="36" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>