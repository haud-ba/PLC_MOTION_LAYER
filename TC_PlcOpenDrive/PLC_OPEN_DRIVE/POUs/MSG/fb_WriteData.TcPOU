<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.4">
  <POU Name="fb_WriteData" Id="{f3f927f4-3f6b-4182-9553-c719b7c1ed59}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK fb_WriteData
VAR_INPUT
	bExecute		: BOOL;
	sFileName		: Tc2_System.T_MaxString; (* filename and ending *)
	psData			: POINTER TO STRING(32767);
END_VAR
VAR_OUTPUT
	bBusy				: BOOL;
	bDone				: BOOL;
	bError			: BOOL;
END_VAR
VAR
	diStep			: DINT;
	udiCounter	: UDINT;

	fbFileOpen		: Tc2_System.FB_FileOpen;
	fbFileWrite		: Tc2_System.FB_FileWrite;
	fbFileClose		: Tc2_System.FB_FileClose;

	sTimeStart		: STRING;
	sTimeEnd			: STRING;
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[udiCounter := udiCounter + 1;

(* pointer check *)
IF (psData = 0)
THEN
	RETURN;
END_IF

IF NOT bExecute
THEN
	diStep := 0;
END_IF

CASE diStep OF

	0:
		fbFileOpen.bExecute		:= FALSE;
		fbFileWrite.bExecute	:= FALSE;
		fbFileClose.bExecute	:= FALSE;
		bBusy := FALSE;
		bDone := FALSE;
		IF bExecute
		THEN
			sTimeStart := GVL_AXIS_CTRL.gfbLocalTime.sLocalTime;
			bBusy  := TRUE;
			diStep := 10;
		END_IF

	10:
		fbFileOpen.bExecute  := TRUE;
		IF fbFileOpen.bBusy
		THEN
			diStep := 20;
		END_IF

	20:
		IF NOT fbFileOpen.bBusy
		THEN
			fbFileOpen.bExecute  := FALSE;
			fbFileWrite.bExecute := TRUE;
			diStep := 30;
		END_IF

	30:
		IF fbFileWrite.bBusy
		THEN
			diStep := 40;
		END_IF

	40:
		IF NOT fbFileWrite.bBusy
		THEN
			fbFileWrite.bExecute := FALSE;
			fbFileClose.bExecute := TRUE;
			diStep := 50;
		END_IF

	50:
		IF fbFileClose.bBusy
		THEN
			diStep := 60;
		END_IF

	60:
		IF NOT fbFileClose.bBusy
		THEN
			fbFileClose.bExecute := FALSE;
			sTimeEnd 	:= GVL_AXIS_CTRL.gfbLocalTime.sLocalTime;
			diStep 		:= 100;
		END_IF
	
	100:
		bBusy := FALSE;
		bDone := TRUE;
END_CASE


fbFileOpen(
	sNetId:= , 
	sPathName:= sFileName, 
	nMode:= FOPEN_MODEAPPEND, 
	ePath:= PATH_GENERIC, 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> , 
	hFile=> );

fbFileWrite(
	sNetId:= , 
	hFile:= fbFileOpen.hFile, 
	pWriteBuff:= psData, 
	cbWriteLen:= f_Len(psData), 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> , 
	cbWrite=> );

fbFileClose(
	sNetId:= , 
	hFile:= fbFileOpen.hFile, 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> );

bError := fbFileOpen.bError OR fbFileWrite.bError;
]]></ST>
    </Implementation>
    <LineIds Name="fb_WriteData">
      <LineId Id="376" Count="0" />
      <LineId Id="306" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="16" Count="2" />
      <LineId Id="14" Count="0" />
      <LineId Id="314" Count="0" />
      <LineId Id="316" Count="2" />
      <LineId Id="315" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="213" Count="2" />
      <LineId Id="217" Count="1" />
      <LineId Id="284" Count="0" />
      <LineId Id="307" Count="0" />
      <LineId Id="240" Count="1" />
      <LineId Id="434" Count="0" />
      <LineId Id="283" Count="0" />
      <LineId Id="402" Count="0" />
      <LineId Id="406" Count="0" />
      <LineId Id="220" Count="0" />
      <LineId Id="89" Count="1" />
      <LineId Id="259" Count="7" />
      <LineId Id="401" Count="0" />
      <LineId Id="268" Count="0" />
      <LineId Id="497" Count="0" />
      <LineId Id="270" Count="10" />
      <LineId Id="464" Count="0" />
      <LineId Id="282" Count="0" />
      <LineId Id="496" Count="0" />
      <LineId Id="288" Count="2" />
      <LineId Id="308" Count="5" />
      <LineId Id="291" Count="3" />
      <LineId Id="493" Count="0" />
      <LineId Id="295" Count="1" />
      <LineId Id="59" Count="0" />
      <LineId Id="301" Count="0" />
      <LineId Id="297" Count="0" />
      <LineId Id="348" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="304" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="131" Count="30" />
      <LineId Id="101" Count="0" />
      <LineId Id="286" Count="0" />
      <LineId Id="200" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>